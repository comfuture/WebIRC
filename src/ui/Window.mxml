<?xml version="1.0" encoding="utf-8"?>
<s:NavigatorContent xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%" currentState="server"
		 creationPolicy="all" creationComplete="init()">
	<fx:Script>
		<![CDATA[
			import flashx.textLayout.edit.SelectionFormat;
			import flashx.textLayout.elements.FlowElement;
			import flashx.textLayout.elements.TextFlow;
			import flashx.textLayout.formats.Direction;
			
			import maroo.net.irc.IRCChannel;
			import maroo.net.irc.IRCMessage;
			import maroo.net.irc.IRCPrefix;
			import maroo.net.irc.IRCUser;
			import maroo.net.irc.IRCUserCollection;
			
			import mx.collections.ArrayCollection;
			import mx.utils.ArrayUtil;
			
			import ui.renderer.UserRenderer;
			import ui.util.MessageFormatter;
			
			private var _prefix:IRCPrefix;

			[Bindable]
			public var users:IRCUserCollection;

			[Bindable]
			public function get prefix():IRCPrefix
			{
				return _prefix;
			}
			public function set prefix(value:IRCPrefix):void
			{
				_prefix = value;
				currentState = value.type;
				if (value is IRCUser) {
					topic = 'Private chat with ' + value.name;
				}
				if (value is IRCChannel) {
					users = new IRCUserCollection((value as IRCChannel).users);
					//lstUsers.dataProvider = users;
				}
			}
			
			override public function get label():String
			{
				return prefix.name;
			}
			
			private function init():void
			{
				history.addEventListener(Event.ADDED, function(event:Event):void {
					scroller.verticalScrollBar.value = scroller.verticalScrollBar.maximum;
				});
			}

			public function appendMessage(message:IRCMessage):void
			{
				callLater(function():void {
					var el:FlowElement = MessageFormatter.formatMessage(message);
					history.textFlow.addChild(el);
				});
			}
			
			public function appendText(text:String):void
			{
				callLater(function():void {
					var el:FlowElement = MessageFormatter.formatString(text);
					history.textFlow.addChild(el);
					//scroller.verticalScrollBar.value = scroller.verticalScrollBar.maximum;
				});
			}
			
			public function addUser(user:IRCUser):void
			{
				users.addUser(user);
			}
			
			public function removeUser(user:IRCUser):void
			{
				users.removeUser(user);
			}
			
			public function refreshUsers():void
			{
				users.source = (prefix as IRCChannel).users;
				lstUsers.dataProvider = users;
				users.refresh();
			}
			
			public function get topic():String
			{
				return txtTopic.text;
			}
			
			public function set topic(value:String):void
			{
				callLater(function():void {
					txtTopic.text = value;
				});
			}
		]]>
	</fx:Script>
	<s:states>
		<s:State name="channel"/>
		<s:State name="user"/>
		<s:State name="server"/>
	</s:states>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:layout>
		<s:HorizontalLayout/>
	</s:layout>
	
	<s:Group width="100%" height="100%" id="view">
		<s:layout>
			<s:VerticalLayout/>
		</s:layout>
		<s:Label id="txtTopic" width="100%" height="20" verticalAlign="middle" paddingLeft="10"
				 backgroundColor="0xEEEEEE" includeIn="channel,user"/>
		<s:Scroller width="100%" height="100%" id="scroller" minViewportInset="1" focusEnabled="false">
			<s:Group width="100%" height="100%" clipAndEnableScrolling="true" id="chatView">
				<s:RichEditableText id="history" width="100%" paddingLeft="10" paddingTop="5" editable="false"
									focusEnabled="false">
				</s:RichEditableText>
			</s:Group>
		</s:Scroller>
	</s:Group>
	<s:List width="200" height="100%" id="lstUsers" dataProvider="{users}"
			itemRenderer="ui.renderer.UserRenderer" includeIn="channel"></s:List>
</s:NavigatorContent>
